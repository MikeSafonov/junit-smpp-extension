buildscript {
    configurations.maybeCreate("pitest")
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.6'
        pitest 'org.pitest:pitest-junit5-plugin:0.11'
    }
}

plugins {
    id "java"
    id "jacoco"
    id "org.sonarqube" version "2.8"
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "info.solidsoft.pitest"

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.1'
}

repositories {
    mavenCentral()
}

group = "com.github.mikesafonov"
version "1.0.0"

jar {
    enabled = true
}

dependencies {
    compileOnly("org.junit.jupiter:junit-jupiter-api:5.5.2")
    implementation("org.restcomm.smpp:ch-smpp:5.1.0-113")

    testImplementation("org.junit.jupiter:junit-jupiter:5.5.2")
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

javadoc {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

pitest {
    testPlugin = "junit5"
    targetClasses = ['com.github.mikesafonov.junit.smpp.*']
    threads = 4
    outputFormats = ['HTML', 'XML']
    enableDefaultIncrementalAnalysis = true
    timestampedReports = false
    historyInputLocation = ".pitest/pitHistory.txt"
    historyOutputLocation = ".pitest/pitHistory.txt"
}


// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            // Add required metadata to POM
            pom.project {
                name "junit-smpp-extension"
                packaging "jar"
                description "JUnit 5 extension to test smpp connection"
                url "https://github.com/MikeSafonov/junit-smpp-extension"
                organization {
                    name "com.github.mikesafonov"
                    url "https://github.com/MikeSafonov"
                }
                issueManagement {
                    system "GitHub"
                    url "https://github.com/MikeSafonov/junit-smpp-extension/issues"
                }
                licenses {
                    license {
                        name "MIT"
                        url "https://github.com/MikeSafonov/junit-smpp-extension/blob/master/LICENSE"
                        distribution "repo"
                    }
                }
                scm {
                    url "https://github.com/MikeSafonov/junit-smpp-extension"
                    connection "scm:git:git://github.com/MikeSafonov/junit-smpp-extension.git"
                    developerConnection "scm:git:ssh://git@github.com:MikeSafonov/junit-smpp-extension.git"
                }
                developers {
                    developer {
                        name "Mike Safonov"
                        organization "com.github.mikesafonov"
                        organizationUrl "https://github.com/MikeSafonov"
                    }
                }
            }
        }
    }
}
